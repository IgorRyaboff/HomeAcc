
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьТокенИВключитьСервис(Знач НовыйТокен) Экспорт
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		РазделяемаяБлокировкаКонстанты = Блокировка.Добавить("Константа.СервисЗаполненияРасходовИзЧеков");
		РазделяемаяБлокировкаКонстанты.Режим = РежимБлокировкиДанных.Разделяемый;
		Блокировка.Заблокировать();
		
		Попытка
			СервисЗаполненияРасходовИзЧеков.ПроверитьТокен(НовыйТокен);
		Исключение
			ТекстОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Проверка токена завершилась ошибкой:
                                              |%1'"), ТекстОшибки);
		КонецПопытки;
		
		Константы.СервисЗаполненияРасходовИзЧеков.Установить(Истина);
		СервисЗаполненияРасходовИзЧеков.ЗаписатьТокен(НовыйТокен);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

#КонецОбласти

#КонецЕсли
