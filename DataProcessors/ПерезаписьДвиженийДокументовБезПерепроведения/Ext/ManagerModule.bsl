
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет гиперссылку для открытия этой обработки
// в форму Обслуживание панели администрирования БСП
//
// Вызывается из НастройкиПрограммыПереопределяемый.ОбслуживаниеПриСозданииНаСервере
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения -
Процедура ДобавитьГиперссылкуВФормуОбслуживание(Форма) Экспорт
	ГруппаГрупповоеИзменение = Форма.Элементы.Найти("ГруппаОбработкаГрупповоеИзменениеОбъектов");
	Если ГруппаГрупповоеИзменение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Гиперссылка = Форма.Элементы.Добавить("ПятьДенегПерезаписьДвижений", Тип("ДекорацияФормы"),
		ГруппаГрупповоеИзменение);
	//
	Гиперссылка.Вид = ВидДекорацииФормы.Надпись;
	Гиперссылка.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	
	ОбъектМетаданных = Метаданные.Обработки.ПерезаписьДвиженийДокументовБезПерепроведения;
	ПредставлениеОбъекта = ОбщегоНазначения.ПредставлениеОбъекта(ОбъектМетаданных);
	
	Гиперссылка.Заголовок = Новый ФорматированнаяСтрока(ПредставлениеОбъекта,,,,
		"e1cib/app/Обработка.ПерезаписьДвиженийДокументовБезПерепроведения");
	// BSLLS:LineLength-off неделимый текст для пользователя
	Гиперссылка.Подсказка = НСтр("ru='Позволяет перезаписать движения регистров произвольных документов, не перепроводя документы целиком'");
	// BSLLS:LineLength-on
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПерезаписатьДвижения(Ссылки, ПерезаписываемыеРегистры) Экспорт
	Для Сч = 0 По Ссылки.ВГраница() Цикл
		Ссылка = Ссылки[Сч];
		
		Прогресс = Цел(Сч / Ссылки.Количество() * 100);
		ДлительныеОперации.СообщитьПрогресс(Прогресс);
		
		Если ЗначениеЗаполнено(Ссылка) И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Проведен") Тогда
			ПроведениеДокументов.ПерезаписатьДвиженияБезПерепроведения(Ссылка, ПерезаписываемыеРегистры);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#КонецЕсли
