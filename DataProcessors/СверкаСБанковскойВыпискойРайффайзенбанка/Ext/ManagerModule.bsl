
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ПрочитатьВыписку(АдресФайла) Экспорт
	ВыпискаДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайла);
	УдалитьИзВременногоХранилища(АдресФайла);
	
	ВыпискаСтрокой = ПолучитьСтрокуИзДвоичныхДанных(ВыпискаДвоичныеДанные, КодировкаТекста.ANSI);
	СтрокиВыписки = РазобратьCSV(ВыпискаСтрокой);
	
	НомераКолонок = Новый Соответствие;
	
	Для Сч = 0 По СтрокиВыписки[0].ВГраница() Цикл
		НомераКолонок[СтрокиВыписки[0][Сч]] = Сч;
	КонецЦикла;
	СтрокиВыписки.Удалить(0);
	
	ОбязательныеКолонки = Новый Массив;
	ОбязательныеКолонки.Добавить("Дата операции");
	ОбязательныеКолонки.Добавить("Описание");
	ОбязательныеКолонки.Добавить("Сумма в валюте счета");
	Для Каждого Колонка Из ОбязательныеКолонки Цикл
		Если НомераКолонок[Колонка] = Неопределено Тогда
			Шаблон = НСтр("ru='В файле отсутствует колонка ""%1"". Вероятно, файл не является выпиской по карте'");
			ВызватьИсключение СтрШаблон(Шаблон, Колонка);
		КонецЕсли;
	КонецЦикла;
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	Результат.Колонки.Добавить("Описание", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Сумма", Метаданные.ОпределяемыеТипы.ДенежнаяСуммаЛюбогоЗнака.Тип);
	
	Для Каждого Строка Из СтрокиВыписки Цикл
		СтрРезультата = Результат.Добавить();
		
		ДатаПоМосковскомуВремени = Дата(Строка[НомераКолонок["Дата операции"]] + ":00");
		УниверсальнаяДата = УниверсальноеВремя(ДатаПоМосковскомуВремени, "GMT+3");
		МестноеВремя = МестноеВремя(УниверсальнаяДата, ЧасовойПоясСеанса());
		
		СтрРезультата.Дата = МестноеВремя;
		СтрРезультата.Описание = Строка[НомераКолонок["Описание"]];
		СтрРезультата.Сумма = СтрЗаменить(Строка[НомераКолонок["Сумма в валюте счета"]], " ", "");
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Функция РазобратьCSV(Текст)
	СтрокиТекста = СтрРазделить(Текст, Символы.ПС);
	РазобранныеСтроки = Новый Массив;
	
	Для Каждого Строка Из СтрокиТекста Цикл
		Если ПустаяСтрока(Строка) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаСНулевымиРазделителями = СтрЗаменитьПоРегулярномуВыражению(Строка,
			";(?=([^""]*""[^""]*"")*(?![^""]*""))", Символ(0));
		//
		
		РазобранныеСтроки.Добавить(СтрРазделить(СтрокаСНулевымиРазделителями, Символ(0)));
	КонецЦикла;
	
	Возврат РазобранныеСтроки;
КонецФункции

#КонецОбласти

#КонецЕсли
