
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Долг") Тогда
		ЗаполнитьПоДолгу(ДанныеЗаполнения);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	ПроведениеДокументов.СоздатьИЗаписатьДвиженияПриПроведении(ЭтотОбъект, "Долги");
	
	НовыйОстаток = Долги.ТекущийОстатокПоДолгу(Долг);
	Если НовыйОстаток < 0 Тогда
		Шаблон = НСтр("ru='Превышен остаток по долгу на %1 руб.'");
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(Шаблон, -НовыйОстаток),,,, Отказ);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	ОбщегоНазначенияПятьДенег.УстановитьПризнакЗаписыватьДляВсехНаборовДвижений(ЭтотОбъект);
	Движения.Записать();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоДолгу(ДолгСсылка)
	Документы.ПогашениеДолга.ЗаполнитьНовыйОбъект(ЭтотОбъект);
	
	Долг = ДолгСсылка;
	СуммаДокумента = Долги.ТекущийОстатокПоДолгу(Долг);
КонецПроцедуры

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
