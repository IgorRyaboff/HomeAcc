
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует объект значениями по умолчанию
//
// Параметры:
//  Объект - ДокументОбъект.РучноеДвижениеПоАналитике, ДанныеФормыСтруктура -
Процедура ЗаполнитьНовыйОбъект(Объект) Экспорт
	Объект.Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

// Формирует наборы движений по регистрам для переданного документа
//
// Параметры:
//  Ссылка - ДокументСсылка.РучноеДвижениеПоАналитике -
//
// Возвращаемое значение:
//  Структура - Ключ структуры соответствует имени регистра.
//              Значение каждого свойства - ТаблицаЗначений с движениями по регистру
Функция ДанныеДляПроведенияДокумента(Ссылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РучноеДвижениеПоАналитике.Дата КАК Период,
		|	ВЫБОР
		|		КОГДА РучноеДвижениеПоАналитике.Сумма > 0
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ КАК ВидДвижения,
		|	РучноеДвижениеПоАналитике.СтатьяАналитики КАК СтатьяАналитики,
		|	ВЫБОР
		|		КОГДА РучноеДвижениеПоАналитике.Сумма > 0
		|			ТОГДА РучноеДвижениеПоАналитике.Сумма
		|		ИНАЧЕ -РучноеДвижениеПоАналитике.Сумма
		|	КОНЕЦ КАК Сумма
		|ИЗ
		|	Документ.РучноеДвижениеПоАналитике КАК РучноеДвижениеПоАналитике
		|ГДЕ
		|	РучноеДвижениеПоАналитике.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Новый Структура("Аналитики", РезультатЗапроса.Выгрузить());
КонецФункции

#КонецОбласти

#КонецЕсли
