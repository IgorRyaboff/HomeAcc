
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует объект значениями по умолчанию
//
// Параметры:
//  Объект - ДокументОбъект.ПроизвольнаяОперацияПоСчетам, ДанныеФормыСтруктура -
Процедура ЗаполнитьНовыйОбъект(Объект) Экспорт
	Объект.Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ДобавитьКомандыСозданияНаОсновании(Команды, НастройкиФормы) Экспорт
	ДобавитьКомандуСозданияНаОсновании(Команды);
КонецПроцедуры

Функция ДобавитьКомандуСозданияНаОсновании(Команды) Экспорт
	Возврат СозданиеНаОсновании.ДобавитьКомандуСозданияНаОсновании(Команды,
		Метаданные.Документы.ПроизвольнаяОперацияПоСчетам);
	//
КонецФункции

// Добавляет команды отчетов для данного документа
//
// Параметры:
//  КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//  Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	Отчеты.ОстаткиНаСчетах.ДобавитьКомандуОтчета(КомандыОтчетов);
КонецПроцедуры

// Формирует наборы движений по регистрам для переданного документа
//
// Параметры:
//  Ссылка - ДокументСсылка.ПроизвольнаяОперацияПоСчетам -
//
// Возвращаемое значение:
//  Структура - Ключ структуры соответствует имени регистра.
//              Значение каждого свойства - ТаблицаЗначений с движениями по регистру
Функция ДанныеДляПроведенияДокумента(Ссылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПроизвольнаяОперацияПоСчетам.Дата КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ПроизвольнаяОперацияПоСчетам.СчетСписания КАК Счет,
		|	ПроизвольнаяОперацияПоСчетам.СуммаДокумента КАК Сумма,
		|	ПроизвольнаяОперацияПоСчетам.СчетСписания <> ЗНАЧЕНИЕ(Справочник.Счета.ПустаяСсылка)
		|		И ПроизвольнаяОперацияПоСчетам.СчетЗачисления <> ЗНАЧЕНИЕ(Справочник.Счета.ПустаяСсылка) КАК ЭтоПереводМеждуСчетами
		|ИЗ
		|	Документ.ПроизвольнаяОперацияПоСчетам КАК ПроизвольнаяОперацияПоСчетам
		|ГДЕ
		|	ПроизвольнаяОперацияПоСчетам.Ссылка = &Ссылка
		|	И ПроизвольнаяОперацияПоСчетам.СчетСписания <> ЗНАЧЕНИЕ(Справочник.Счета.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПроизвольнаяОперацияПоСчетам.Дата,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
		|	ПроизвольнаяОперацияПоСчетам.СчетЗачисления,
		|	ПроизвольнаяОперацияПоСчетам.СуммаДокумента,
		|	ПроизвольнаяОперацияПоСчетам.СчетСписания <> ЗНАЧЕНИЕ(Справочник.Счета.ПустаяСсылка)
		|		И ПроизвольнаяОперацияПоСчетам.СчетЗачисления <> ЗНАЧЕНИЕ(Справочник.Счета.ПустаяСсылка)
		|ИЗ
		|	Документ.ПроизвольнаяОперацияПоСчетам КАК ПроизвольнаяОперацияПоСчетам
		|ГДЕ
		|	ПроизвольнаяОперацияПоСчетам.Ссылка = &Ссылка
		|	И ПроизвольнаяОперацияПоСчетам.СчетЗачисления <> ЗНАЧЕНИЕ(Справочник.Счета.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Новый Структура("ОстаткиНаСчетах", РезультатЗапроса.Выгрузить());
КонецФункции

#КонецОбласти

#КонецЕсли
