
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Доход") Тогда
		ЗаполнитьПоДоходу(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Расход") Тогда
		ЗаполнитьПоРасходу(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтменаРасхода") Тогда
		ЗаполнитьПоОтменеРасхода(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПроизвольнаяОперацияПоСчетам") Тогда
		ЗаполнитьПоПроизвольнойОперации(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПогашениеДолга") Тогда
		ЗаполнитьПоПогашениюДолга(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("ЭтоВыпискаРайффайзенбанка") Тогда
		ЗаполнитьПоВыпискеРайффайзенбанка(ДанныеЗаполнения);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	ПроведениеДокументов.СоздатьИЗаписатьДвиженияПриПроведении(ЭтотОбъект);
	
	ДоходыРасходы.ПроверитьОстатокНаСчетеПриПроведении(СчетСписания, Новый Граница(Дата, ВидГраницы.Включая), Отказ);
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	ОбщегоНазначенияПятьДенег.УстановитьПризнакЗаписыватьДляВсехНаборовДвижений(ЭтотОбъект);
	Движения.Записать();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоВыпискеРайффайзенбанка(ДанныеЗаполнения)
	Документы.ПроизвольнаяОперацияПоСчетам.ЗаполнитьНовыйОбъект(ЭтотОбъект);
	
	Дата = ДанныеЗаполнения.Дата;
	СуммаДокумента = ДанныеЗаполнения.Сумма;
КонецПроцедуры

Процедура ЗаполнитьПоПогашениюДолга(ДанныеЗаполнения)
	Документы.ПроизвольнаяОперацияПоСчетам.ЗаполнитьНовыйОбъект(ЭтотОбъект);
	ДокументОснование = ДанныеЗаполнения;
	СуммаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "СуммаДокумента");
КонецПроцедуры

Процедура ЗаполнитьПоДоходу(ДанныеЗаполнения)
	Документы.ПроизвольнаяОперацияПоСчетам.ЗаполнитьНовыйОбъект(ЭтотОбъект);
	
	ДокументОснование = ДанныеЗаполнения;
		
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Счет, СуммаДокумента");
	СчетЗачисления = РеквизитыДокумента.Счет;
	СуммаДокумента = РеквизитыДокумента.СуммаДокумента;
КонецПроцедуры

Процедура ЗаполнитьПоРасходу(ДанныеЗаполнения)
	Документы.ПроизвольнаяОперацияПоСчетам.ЗаполнитьНовыйОбъект(ЭтотОбъект);
	
	ДокументОснование = ДанныеЗаполнения;
		
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Счет, СуммаДокумента");
	СчетСписания = РеквизитыДокумента.Счет;
	СуммаДокумента = РеквизитыДокумента.СуммаДокумента;
КонецПроцедуры

Процедура ЗаполнитьПоОтменеРасхода(ДанныеЗаполнения)
	Документы.ПроизвольнаяОперацияПоСчетам.ЗаполнитьНовыйОбъект(ЭтотОбъект);
	
	ДокументОснование = ДанныеЗаполнения;
		
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Счет, СуммаДокумента");
	СчетЗачисления = РеквизитыДокумента.Счет;
	СуммаДокумента = РеквизитыДокумента.СуммаДокумента;
КонецПроцедуры

Процедура ЗаполнитьПоПроизвольнойОперации(ДанныеЗаполнения)
	Документы.ПроизвольнаяОперацияПоСчетам.ЗаполнитьНовыйОбъект(ЭтотОбъект);
	
	ДокументОснование = ДанныеЗаполнения;
		
	СписокРеквизитов = "СчетСписания, СчетЗачисления, СуммаДокумента";
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, СписокРеквизитов);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыДокумента, СписокРеквизитов);
КонецПроцедуры

#КонецОбласти

#КонецЕсли
