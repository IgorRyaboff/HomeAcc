
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Расход") Тогда
		ЗаполнитьПоРасходу(ДанныеЗаполнения);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Для Каждого СтрРасходы Из Расходы Цикл
		Если СтрРасходы.ОтмененноеКоличество > СтрРасходы.ИсходноеКоличество Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон(НСтр("ru = 'Отмененное количество в строке %1 больше исходного'"),
				СтрРасходы.НомерСтроки);
			//
			Сообщение.Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Расходы",
				СтрРасходы.НомерСтроки, "ОтмененноеКоличество");
			//
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
        Возврат;
    КонецЕсли;
    
    ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	СуммаДокумента = Расходы.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	ПроведениеДокументов.СоздатьИЗаписатьДвиженияПриПроведении(ЭтотОбъект);
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	ОбщегоНазначенияПятьДенег.УстановитьПризнакЗаписыватьДляВсехНаборовДвижений(ЭтотОбъект);
	Движения.Записать();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоРасходу(Ссылка)
	Документы.ОтменаРасхода.ЗаполнитьНовыйОбъект(ЭтотОбъект);
	
	ДокументОснование = Ссылка;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходРасходы.Номенклатура КАК Номенклатура,
		|	РасходРасходы.КатегорияРасходов КАК КатегорияРасходов,
		|	РасходРасходы.Цена КАК Цена,
		|	РасходРасходы.КоличествоВУпаковке * РасходРасходы.КоличествоУпаковок КАК ИсходноеКоличество
		|ИЗ
		|	Документ.Расход.Расходы КАК РасходРасходы
		|ГДЕ
		|	РасходРасходы.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	РасходРасходы.НомерСтроки";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Расходы.Загрузить(РезультатЗапроса.Выгрузить());
КонецПроцедуры

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
