
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует объект значениями по умолчанию
//
// Параметры:
//  Объект - ДокументОбъект.ОтменаРасхода, ДанныеФормыСтруктура -
Процедура ЗаполнитьНовыйОбъект(Объект) Экспорт
	Объект.Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	Документы.ПроизвольнаяОперацияПоСчетам.ДобавитьКомандуСозданияНаОсновании(КомандыСозданияНаОсновании);
КонецПроцедуры

Функция ДобавитьКомандуСозданияНаОсновании(Команды) Экспорт
	Возврат СозданиеНаОсновании.ДобавитьКомандуСозданияНаОсновании(Команды, Метаданные.Документы.ОтменаРасхода);
КонецФункции

// Добавляет команды отчетов для данного документа
//
// Параметры:
//  КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//  Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	Отчеты.ОстаткиНаСчетах.ДобавитьКомандуОтчета(КомандыОтчетов);
КонецПроцедуры

// Формирует наборы движений по регистрам для переданного документа
//
// Параметры:
//  Ссылка - ДокументСсылка.ОтменаРасхода -
//
// Возвращаемое значение:
//  Структура - Ключ структуры соответствует имени регистра.
//              Значение каждого свойства - ТаблицаЗначений с движениями по регистру
Функция ДанныеДляПроведенияДокумента(Ссылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтменаРасхода.Дата КАК Период,
		|	ВЫБОР
		|		КОГДА ПравилаВеденияАналитик.ВидДвижения = ""Приход""
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ КАК ВидДвижения,
		|	ПравилаВеденияАналитик.СтатьяАналитики КАК СтатьяАналитики,
		|	СУММА(-ОтменаРасходаРасходы.Сумма) КАК Сумма
		|ИЗ
		|	Документ.ОтменаРасхода.Расходы КАК ОтменаРасходаРасходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтменаРасхода КАК ОтменаРасхода
		|		ПО ОтменаРасходаРасходы.Ссылка = ОтменаРасхода.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПравилаВеденияАналитик КАК ПравилаВеденияАналитик
		|		ПО (ОтменаРасходаРасходы.СтатьяРасходов = (ВЫРАЗИТЬ(ПравилаВеденияАналитик.СтатьяДоходовРасходов КАК Справочник.СтатьиРасходов)))
		|ГДЕ
		|	ОтменаРасходаРасходы.Ссылка = &Ссылка
		|	И ОтменаРасходаРасходы.ОтмененноеКоличество > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ПравилаВеденияАналитик.СтатьяАналитики,
		|	ОтменаРасхода.Дата,
		|	ВЫБОР
		|		КОГДА ПравилаВеденияАналитик.ВидДвижения = ""Приход""
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ОтменаРасхода.Дата КАК Период,
		|	ОтменаРасхода.Счет КАК Счет,
		|	ОтменаРасхода.СуммаДокумента КАК Сумма
		|ИЗ
		|	Документ.ОтменаРасхода КАК ОтменаРасхода
		|ГДЕ
		|	ОтменаРасхода.Ссылка = &Ссылка
		|	И ОтменаРасхода.Счет <> ЗНАЧЕНИЕ(Справочник.Счета.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтменаРасхода.Дата КАК Период,
		|	ОтменаРасходаРасходы.КатегорияРасходов КАК КатегорияРасходов,
		|	ОтменаРасходаРасходы.Номенклатура КАК Номенклатура,
		|	ОтменаРасходаРасходы.СтатьяРасходов КАК СтатьяРасходов,
		|	ОтменаРасхода.Счет КАК Счет,
		|	СУММА(-ОтменаРасходаРасходы.Сумма) КАК Сумма,
		|	СУММА(ВЫБОР
		|			КОГДА СпрНоменклатура.ЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|					И НЕ СпрНоменклатура.ЕдиницаИзмерения.Безразмерная
		|				ТОГДА -ОтменаРасходаРасходы.ОтмененноеКоличество
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Количество
		|ИЗ
		|	Документ.ОтменаРасхода.Расходы КАК ОтменаРасходаРасходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтменаРасхода КАК ОтменаРасхода
		|		ПО ОтменаРасходаРасходы.Ссылка = ОтменаРасхода.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО ОтменаРасходаРасходы.Номенклатура = СпрНоменклатура.Ссылка
		|ГДЕ
		|	ОтменаРасхода.Ссылка = &Ссылка
		|	И ОтменаРасходаРасходы.ОтмененноеКоличество > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ОтменаРасхода.Дата,
		|	ОтменаРасходаРасходы.КатегорияРасходов,
		|	ОтменаРасходаРасходы.Номенклатура,
		|	ОтменаРасходаРасходы.СтатьяРасходов,
		|	ОтменаРасхода.Счет,
		|	ОтменаРасходаРасходы.ОтмененноеКоличество";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Движения = Новый Структура;
	
	Движения.Вставить("Аналитики", РезультатЗапроса[0].Выгрузить());
	Движения.Вставить("ОстаткиНаСчетах", РезультатЗапроса[1].Выгрузить());
	Движения.Вставить("Расходы", РезультатЗапроса[2].Выгрузить());
	
	Возврат Движения;
КонецФункции

#КонецОбласти

#КонецЕсли
