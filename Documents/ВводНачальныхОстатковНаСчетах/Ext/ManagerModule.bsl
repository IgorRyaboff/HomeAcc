
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует объект значениями по умолчанию
//
// Параметры:
//  Объект - ДокументОбъект.ВводНачальныхОстатковНаСчетах, ДанныеФормыСтруктура -
Процедура ЗаполнитьНовыйОбъект(Объект) Экспорт
	Объект.Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

// Добавляет команды отчетов для данного документа
//
// Параметры:
//  КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//  Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	Отчеты.ОстаткиНаСчетах.ДобавитьКомандуОтчета(КомандыОтчетов);
КонецПроцедуры

// Формирует наборы движений по регистрам для переданного документа
//
// Параметры:
//  Ссылка - ДокументСсылка.ВводНачальныхОстатковНаСчетах -
//
// Возвращаемое значение:
//  Структура - Ключ структуры соответствует имени регистра.
//              Значение каждого свойства - ТаблицаЗначений с движениями по регистру
Функция ДанныеДляПроведенияДокумента(Ссылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	// BSLLS:VirtualTableCallWithoutParameters-off
	// BSLLS:JoinWithVirtualTable-off
	// В запросе соединение не с виртуальной таблицей, а с табличной частью Остатки
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ВводНачальныхОстатковНаСчетах.Дата КАК Период,
		|	ВводНачальныхОстатковНаСчетахОстатки.Счет КАК Счет,
		|	СУММА(ВводНачальныхОстатковНаСчетахОстатки.Сумма) КАК Сумма
		|ИЗ
		|	Документ.ВводНачальныхОстатковНаСчетах.Остатки КАК ВводНачальныхОстатковНаСчетахОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводНачальныхОстатковНаСчетах КАК ВводНачальныхОстатковНаСчетах
		|		ПО ВводНачальныхОстатковНаСчетахОстатки.Ссылка = ВводНачальныхОстатковНаСчетах.Ссылка
		|ГДЕ
		|	ВводНачальныхОстатковНаСчетах.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВводНачальныхОстатковНаСчетах.Дата,
		|	ВводНачальныхОстатковНаСчетахОстатки.Счет";
	// BSLLS:JoinWithVirtualTable-on
	// BSLLS:VirtualTableCallWithoutParameters-on
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Новый Структура("ОстаткиНаСчетах", РезультатЗапроса.Выгрузить());
КонецФункции

#КонецОбласти

#КонецЕсли
