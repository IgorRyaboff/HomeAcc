
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует объект значениями по умолчанию
//
// Параметры:
//  Объект - ДокументОбъект.ОценкаЛишнихРасходов, ДанныеФормыСтруктура -
Процедура ЗаполнитьНовыйОбъект(Объект) Экспорт
	Объект.Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

// Обработчик заполнения списка текущих дел
// Параметры:
//   ТекущиеДела - см. ТекущиеДелаСервер.ТекущиеДела.
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	Если Не ПравоДоступа("Редактирование", Метаданные.Документы.ОценкаЛишнихРасходов) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходыОбороты.Период КАК Период
		|ИЗ
		|	РегистрНакопления.Расходы.Обороты(, &КонецПрошлогоМесяца, Месяц, ) КАК РасходыОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЛишниеРасходы.Обороты(, , Месяц, ) КАК ЛишниеРасходыОбороты
		|		ПО РасходыОбороты.Период = ЛишниеРасходыОбороты.Период
		|ГДЕ
		|	ЛишниеРасходыОбороты.Период ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	РасходыОбороты.Период";
	
	Запрос.УстановитьПараметр("КонецПрошлогоМесяца", НачалоМесяца(ТекущаяДатаСеанса()) - 1);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	НезаполненныеПериоды = РезультатЗапроса[0].Выгрузить().ВыгрузитьКолонку("Период");
	
	ГруппаДел = ТекущиеДела.Добавить();
	ГруппаДел.Владелец = Метаданные.Подсистемы.ДоходыРасходы;
	ГруппаДел.Идентификатор = "ОценкаЛишнихРасходов_НеСозданные";
	ГруппаДел.ЕстьДела = (НезаполненныеПериоды.Количество() > 0);
	ГруппаДел.Представление = НСтр("ru='Не созданы оценки лишних расходов'");
	
	Для Каждого Период Из НезаполненныеПериоды Цикл
		Дело = ТекущиеДела.Добавить();
		Дело.Владелец = ГруппаДел.Идентификатор;
		Дело.Идентификатор = "ОценкаЛишнихРасходов_НеСозданные_" + Формат(Период, НСтр("ru = 'ДФ=yyyyMM'"));
		Дело.ЕстьДела = Истина;
		Дело.СкрыватьВНастройках = Истина;
		Дело.Представление = Формат(Период, НСтр("ru = 'ДФ=''MMMM yyyy'''"));
		
		Дело.Форма = "Документ.ОценкаЛишнихРасходов.ФормаОбъекта";
		Дело.ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", Новый Структура("Месяц", Период));
	КонецЦикла;
КонецПроцедуры

// Добавляет команды отчетов для данного документа
//
// Параметры:
//  КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//  Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	Отчеты.ЛишниеРасходы.ДобавитьКомандуОтчета(КомандыОтчетов);
КонецПроцедуры

// Формирует наборы движений по регистрам для переданного документа
//
// Параметры:
//  Ссылка - ДокументСсылка.ОценкаЛишнихРасходов -
//
// Возвращаемое значение:
//  Структура - Ключ структуры соответствует имени регистра.
//              Значение каждого свойства - ТаблицаЗначений с движениями по регистру
Функция ДанныеДляПроведенияДокумента(Ссылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОценкаЛишнихРасходов.Месяц КАК Период,
		|	ОценкаЛишнихРасходовЛишниеРасходы.КатегорияРасходов КАК КатегорияРасходов,
		|	ОценкаЛишнихРасходовЛишниеРасходы.Номенклатура КАК Номенклатура,
		|	ОценкаЛишнихРасходовЛишниеРасходы.СтатьяРасходов КАК СтатьяРасходов,
		|	ОценкаЛишнихРасходовЛишниеРасходы.Сумма КАК Сумма
		|ИЗ
		|	Документ.ОценкаЛишнихРасходов.ЛишниеРасходы КАК ОценкаЛишнихРасходовЛишниеРасходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОценкаЛишнихРасходов КАК ОценкаЛишнихРасходов
		|		ПО ОценкаЛишнихРасходовЛишниеРасходы.Ссылка = ОценкаЛишнихРасходов.Ссылка
		|ГДЕ
		|	ОценкаЛишнихРасходовЛишниеРасходы.Ссылка = &Ссылка
		|	И ОценкаЛишнихРасходовЛишниеРасходы.Пометка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Новый Структура("ЛишниеРасходы", РезультатЗапроса.Выгрузить());
КонецФункции

#КонецОбласти

#КонецЕсли
