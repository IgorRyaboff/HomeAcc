
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует объект значениями по умолчанию
//
// Параметры:
//  Объект - ДокументОбъект.Доход, ДанныеФормыСтруктура -
Процедура ЗаполнитьНовыйОбъект(Объект) Экспорт
	Объект.Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ДобавитьКомандыСозданияНаОсновании(Команды, НастройкиФормы) Экспорт
	Документы.ПроизвольнаяОперацияПоСчетам.ДобавитьКомандуСозданияНаОсновании(Команды);
КонецПроцедуры

// Добавляет команды отчетов для данного документа
//
// Параметры:
//  КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//  Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	Отчеты.ОстаткиНаСчетах.ДобавитьКомандуОтчета(КомандыОтчетов);
КонецПроцедуры

// Формирует наборы движений по регистрам для переданного документа
//
// Параметры:
//  Ссылка - ДокументСсылка.Доход -
//
// Возвращаемое значение:
//  Структура - Ключ структуры соответствует имени регистра.
//              Значение каждого свойства - ТаблицаЗначений с движениями по регистру
Функция ДанныеДляПроведенияДокумента(Ссылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Доход.Дата КАК Период,
		|	ВЫБОР
		|		КОГДА ПравилаВеденияАналитик.ВидДвижения = ""Приход""
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ КАК ВидДвижения,
		|	ПравилаВеденияАналитик.СтатьяАналитики КАК СтатьяАналитики,
		|	Доход.СуммаДокумента КАК Сумма
		|ИЗ
		|	Документ.Доход КАК Доход
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПравилаВеденияАналитик КАК ПравилаВеденияАналитик
		|		ПО Доход.СтатьяДоходов = ПравилаВеденияАналитик.СтатьяДоходовРасходов
		|ГДЕ
		|	Доход.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Доход.Дата КАК Период,
		|	Доход.Счет КАК Счет,
		|	Доход.СтатьяДоходов КАК СтатьяДоходов,
		|	Доход.СуммаДокумента КАК Сумма
		|ИЗ
		|	Документ.Доход КАК Доход
		|ГДЕ
		|	Доход.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	Доход.Дата КАК Период,
		|	Доход.Счет КАК Счет,
		|	Доход.СуммаДокумента КАК Сумма
		|ИЗ
		|	Документ.Доход КАК Доход
		|ГДЕ
		|	Доход.Ссылка = &Ссылка
		|	И Доход.Счет <> ЗНАЧЕНИЕ(Справочник.Счета.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Движения = Новый Структура;
	
	Движения.Вставить("Аналитики", РезультатЗапроса[0].Выгрузить());
	Движения.Вставить("Доходы", РезультатЗапроса[1].Выгрузить());
	Движения.Вставить("ОстаткиНаСчетах", РезультатЗапроса[2].Выгрузить());
	
	Возврат Движения;
КонецФункции

#КонецОбласти

#КонецЕсли
