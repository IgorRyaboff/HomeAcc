
// Параметр закрытия формы:
//  Неопределено, КодВозвратаДиалога - При отмене заполнения
//  Структура:
//  * ДанныеОЧеке - Структура - См. ПолучениеДанныхОЧеке.НовыеДанныеОЧеке()
//  * ПеренестиДатуВДокумент - Булево -

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Вариант = "Реквизиты";
	
	Если СредстваБуфераОбмена.ИспользованиеДоступно() Тогда
		ПроверитьСодержимоеБуфераОбменаПриОткрытииАсинх();
	Иначе
		Элементы.ВставитьФотоИзБуфераОбмена.Доступность = Ложь;
	КонецЕсли;
	
	ПеренестиДатуВДокумент = Истина;
	
	НастроитьЭлементыФормыПоВарианту();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантРеквизитыПриИзменении(Элемент)
	НастроитьЭлементыФормыПоВарианту();
КонецПроцедуры

&НаКлиенте
Процедура ВариантСтрокаПриИзменении(Элемент)
	НастроитьЭлементыФормыПоВарианту();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	ДлительнаяОперация = Неопределено;
	Если Вариант = "Реквизиты" Тогда
		ДлительнаяОперация = НачатьПолучениеДанныхОЧекеПоРеквизитам(ФН, ФД, ФП, Дата, Итог, УникальныйИдентификатор);
	ИначеЕсли Вариант = "Строка" Тогда
		ДлительнаяОперация = НачатьПолучениеДанныхОЧекеПоСтрокеQRКода(СтрокаQRКода, УникальныйИдентификатор);
	Иначе
		ВызватьИсключение НСтр("ru='Неизвестный вариант'");
	КонецЕсли;
	
	ОжидатьЗавершениеПолученияДанныхОЧеке(ДлительнаяОперация);
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыбратьФотоИзФайла(Команда)
	Фильтр = НСтр("ru='Изображение|*.png'*.jpg;*.jpeg'");
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов(, Ложь, Фильтр);
	
	Доступность = Ложь;
	ОписаниеПомещенногоФайла = Ждать ПоместитьФайлНаСерверАсинх(,,, ПараметрыДиалога, УникальныйИдентификатор);
	Доступность = Истина;
	
	Если ОписаниеПомещенногоФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = НачатьПолучениеДанныхОЧекеПоФото(ОписаниеПомещенногоФайла.Адрес, УникальныйИдентификатор);
	ОжидатьЗавершениеПолученияДанныхОЧеке(ДлительнаяОперация);
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВставитьФотоИзБуфераОбмена(Команда)
	Если Не СредстваБуфераОбмена.ИспользованиеДоступно() Тогда
		ПоказатьПредупреждение(, НСтр("ru='Работа с буфером обмена недоступна на этом устройстве'"));
		Возврат;
	КонецЕсли;
	
	Картинка = Ждать СредстваБуфераОбмена.ПолучитьДанныеАсинх(СтандартныйФорматДанныхБуфераОбмена.Картинка);
	Если Картинка = Неопределено Тогда
		#Если ВебКлиент Тогда
			ПоказатьПредупреждение(, НСтр("ru='Буфер обмена не содержит изображение, либо программе запрещен доступ к буферу обмена'"));
		#Иначе
			ПоказатьПредупреждение(, НСтр("ru='Буфер обмена не содержит изображение'"));
		#КонецЕсли
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = НачатьПолучениеДанныхОЧекеПоФото(Картинка, УникальныйИдентификатор);
	ОжидатьЗавершениеПолученияДанныхОЧеке(ДлительнаяОперация);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВариантыДлительныхОпераций

&НаСервереБезКонтекста
Функция НачатьПолучениеДанныхОЧекеПоРеквизитам(Знач ФН, Знач ФД, Знач ФП, Знач Дата, Знач Итог, Знач ИдентификаторФормы)
	ПараметрыВыполнения = ПараметрыВыполненияДляПолученияДанныхОЧеке(ИдентификаторФормы);
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "СервисЗаполненияРасходовИзЧеков.ДанныеОЧекеПоРеквизитам", ФН, ФД, ФП, Дата, Итог);
КонецФункции

&НаСервереБезКонтекста
Функция НачатьПолучениеДанныхОЧекеПоСтрокеQRКода(Знач СтрокаQRКода, Знач ИдентификаторФормы)
	ПараметрыВыполнения = ПараметрыВыполненияДляПолученияДанныхОЧеке(ИдентификаторФормы);
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "СервисЗаполненияРасходовИзЧеков.ДанныеОЧекеПоСтрокеQRКода", СтрокаQRКода);
КонецФункции

&НаСервереБезКонтекста
Функция НачатьПолучениеДанныхОЧекеПоФото(Знач КартинкаИлиАдресФайла, Знач ИдентификаторФормы)
	Если ТипЗнч(КартинкаИлиАдресФайла) = Тип("Картинка") Тогда
		ДвоичныеДанныеКартинки = КартинкаИлиАдресФайла.ПолучитьДвоичныеДанные();
	Иначе
		ДвоичныеДанныеКартинки = ПолучитьИзВременногоХранилища(КартинкаИлиАдресФайла);
	КонецЕсли;
	
	ПараметрыВыполнения = ПараметрыВыполненияДляПолученияДанныхОЧеке(ИдентификаторФормы);
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "СервисЗаполненияРасходовИзЧеков.ДанныеОЧекеПоФото", ДвоичныеДанныеКартинки);
КонецФункции

&НаСервереБезКонтекста
Функция ПараметрыВыполненияДляПолученияДанныхОЧеке(Знач ИдентификаторФормы)
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	ПараметрыВыполнения.УточнениеОшибки = НСтр("ru='Не удалось получить данные о чеке по причине:'");
	Возврат ПараметрыВыполнения;
КонецФункции

#КонецОбласти

&НаКлиенте
Процедура ОжидатьЗавершениеПолученияДанныхОЧеке(ДлительнаяОперация)
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеПолученияДанныхОЧеке", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияДанныхОЧеке(РезультатОперации, ДополнительныеПараметры) Экспорт
	ОбщегоНазначенияПятьДенегКлиент.ПоказатьОшибкуДлительнойОперации(РезультатОперации);
	Если Не ОбщегоНазначенияПятьДенегКлиент.ДлительнаяОперацияУспешна(РезультатОперации) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеОЧеке = ПолучитьИзВременногоХранилища(РезультатОперации.АдресРезультата);
	Закрыть(Новый Структура("ДанныеОЧеке, ПеренестиДатуВДокумент", ДанныеОЧеке, ПеренестиДатуВДокумент));
КонецПроцедуры

&НаКлиенте
Процедура НастроитьЭлементыФормыПоВарианту()
	Элементы.ГруппаРеквизитыПоля.Доступность = (Вариант = "Реквизиты");
	Элементы.ГруппаСтрокаПоля.Доступность = (Вариант = "Строка");
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПроверитьСодержимоеБуфераОбменаПриОткрытииАсинх()
	ЕстьКартинка = Ждать СредстваБуфераОбмена.СодержитДанныеАсинх(СтандартныйФорматДанныхБуфераОбмена.Картинка);
	Если ЕстьКартинка Тогда
		Вариант = "Фото";
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
