
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует объект значениями по умолчанию
//
// Параметры:
//  Объект - ДокументОбъект.ПереводМеждуСчетами, ДанныеФормыСтруктура -
Процедура ЗаполнитьНовыйОбъект(Объект) Экспорт
	Объект.Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Функция ДобавитьКомандуСозданияНаОсновании(Команды) Экспорт
	Возврат СозданиеНаОсновании.ДобавитьКомандуСозданияНаОсновании(Команды, Метаданные.Документы.ПереводМеждуСчетами);
КонецФункции

// Добавляет команды отчетов для данного документа
//
// Параметры:
//  КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//  Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	Отчеты.ОстаткиНаСчетах.ДобавитьКомандуОтчета(КомандыОтчетов);
КонецПроцедуры

// Формирует наборы движений по регистрам для переданного документа
//
// Параметры:
//  Ссылка - ДокументСсылка.ПереводМеждуСчетами -
//
// Возвращаемое значение:
//  Структура - Ключ структуры соответствует имени регистра.
//              Значение каждого свойства - ТаблицаЗначений с движениями по регистру
Функция ДанныеДляПроведенияДокумента(Ссылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПереводМеждуСчетами.Дата КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ПереводМеждуСчетами.СчетСписания КАК Счет,
		|	ПереводМеждуСчетами.СуммаДокумента КАК Сумма,
		|	ИСТИНА КАК ЭтоПереводМеждуСчетами
		|ИЗ
		|	Документ.ПереводМеждуСчетами КАК ПереводМеждуСчетами
		|ГДЕ
		|	ПереводМеждуСчетами.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПереводМеждуСчетами.Дата,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
		|	ПереводМеждуСчетами.СчетЗачисления,
		|	ПереводМеждуСчетами.СуммаДокумента,
		|	ИСТИНА
		|ИЗ
		|	Документ.ПереводМеждуСчетами КАК ПереводМеждуСчетами
		|ГДЕ
		|	ПереводМеждуСчетами.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Новый Структура("ОстаткиНаСчетах", РезультатЗапроса.Выгрузить());
КонецФункции

#КонецОбласти

#КонецЕсли
