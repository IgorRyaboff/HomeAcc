
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПерезаполнитьРегистр() Экспорт
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		Блокировка.Добавить("РегистрСведений.ПравилаВеденияАналитик");
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Записать();
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СтатьиАналитики.Ссылка КАК СтатьяАналитики,
			|	СтатьиАналитики.ВидДвиженияДляВсехДоходов КАК ВидДвижения
			|ИЗ
			|	Справочник.СтатьиАналитики КАК СтатьиАналитики
			|ГДЕ
			|	СтатьиАналитики.ВидДвиженияДляВсехДоходов <> """"
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СтатьиАналитики.Ссылка КАК СтатьяАналитики,
			|	СтатьиАналитики.ВидДвиженияДляВсехРасходов КАК ВидДвижения
			|ИЗ
			|	Справочник.СтатьиАналитики КАК СтатьиАналитики
			|ГДЕ
			|	СтатьиАналитики.ВидДвиженияДляВсехРасходов <> """"";
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		НакопленныеПравилаДоходов = РезультатЗапроса[0].Выгрузить();
		НакопленныеПравилаРасходов = РезультатЗапроса[1].Выгрузить();
		
		ПерезаполнитьРегистрИтерация(Справочники.СтатьиДоходов.ПустаяСсылка(), НакопленныеПравилаДоходов);
		ПерезаполнитьРегистрИтерация(Справочники.СтатьиРасходов.ПустаяСсылка(), НакопленныеПравилаРасходов);
	
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПерезаполнитьРегистрИтерация(Родитель, НакопленныеПравила)
	Запрос = Новый Запрос;
	
	Если ТипЗнч(Родитель) = Тип("СправочникСсылка.СтатьиДоходов") Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СтатьиДоходовПравилаВеденияАналитик.СтатьяАналитики КАК СтатьяАналитики,
			|	СтатьиДоходовПравилаВеденияАналитик.ВидДвижения КАК ВидДвижения
			|ИЗ
			|	Справочник.СтатьиДоходов.ПравилаВеденияАналитик КАК СтатьиДоходовПравилаВеденияАналитик
			|ГДЕ
			|	СтатьиДоходовПравилаВеденияАналитик.Ссылка = &Родитель
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СтатьиДоходов.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.СтатьиДоходов КАК СтатьиДоходов
			|ГДЕ
			|	СтатьиДоходов.Родитель = &Родитель";
	ИначеЕсли ТипЗнч(Родитель) = Тип("СправочникСсылка.СтатьиРасходов") Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СтатьиРасходовПравилаВеденияАналитик.СтатьяАналитики КАК СтатьяАналитики,
			|	СтатьиРасходовПравилаВеденияАналитик.ВидДвижения КАК ВидДвижения
			|ИЗ
			|	Справочник.СтатьиРасходов.ПравилаВеденияАналитик КАК СтатьиРасходовПравилаВеденияАналитик
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатьиРасходов КАК СтатьиРасходов
			|		ПО СтатьиРасходовПравилаВеденияАналитик.Ссылка = СтатьиРасходов.Ссылка
			|ГДЕ
			|	СтатьиРасходовПравилаВеденияАналитик.Ссылка = &Родитель
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СтатьиРасходов.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.СтатьиРасходов КАК СтатьиРасходов
			|ГДЕ
			|	СтатьиРасходов.Родитель = &Родитель";
	Иначе
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Неожиданный тип ссылки Родитель: %1'"), ТипЗнч(Родитель));
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Родитель", Родитель);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаНовыеПравила = РезультатЗапроса[0].Выбрать();
	ВыборкаДочерниеСтатьи = РезультатЗапроса[1].Выбрать();
	
	Пока ВыборкаНовыеПравила.Следующий() Цикл
		СтрНакопленныеПравила = НакопленныеПравила.Найти(ВыборкаНовыеПравила.СтатьяАналитики, "СтатьяАналитики");
		Если СтрНакопленныеПравила = Неопределено Тогда
			СтрНакопленныеПравила = НакопленныеПравила.Добавить();
			СтрНакопленныеПравила.СтатьяАналитики = ВыборкаНовыеПравила.СтатьяАналитики;
		КонецЕсли;
		
		СтрНакопленныеПравила.ВидДвижения = ВыборкаНовыеПравила.ВидДвижения;
	КонецЦикла;
	
	Если Не Родитель.Пустая() И НакопленныеПравила.Количество() > 0 Тогда
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Найти("СтатьяДоходовРасходов").Установить(Родитель);
		
		Для Каждого СтрНакопленныеПравила Из НакопленныеПравила Цикл
			Запись = НаборЗаписей.Добавить();
			Запись.СтатьяДоходовРасходов = Родитель;
			ЗаполнитьЗначенияСвойств(Запись, СтрНакопленныеПравила, "СтатьяАналитики, ВидДвижения");
		КонецЦикла;
		
		НаборЗаписей.Записать(Ложь);
	КонецЕсли;
	
	Пока ВыборкаДочерниеСтатьи.Следующий() Цикл
		ПерезаполнитьРегистрИтерация(ВыборкаДочерниеСтатьи.Ссылка, НакопленныеПравила.Скопировать());
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#КонецЕсли
